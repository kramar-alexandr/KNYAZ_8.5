external procedure CLOutVc_PasteCurncyCode(var record CLOutVc);
external procedure CLOutVc_PasteCorspCode(var record CLOutVc,Integer);
external procedure CLOutVc_PasteCurncyCode(var record CLOutVc);
external procedure CLInVc_PasteCurncyCode(var record CLInVc);

SetLangMode(LangRussian,"RUS",0);


procedure addParamToJSONArea(string param, string value, var area req)
begin

AddTextToArea("\"" & param & "\":" &  "\"" & value & "\",",req);

return;
end;

procedure delLastComaInArea(var area req)
begin
	longint alen;
	string 1 lastsymb;
	area tmpar;
	
	alen = getarealength(req);
	lastsymb = getstringfromarea(req,alen-1,1);
	if(lastsymb==",")then begin
		getareafromarea(req,0,alen-1,tmpar);
		setareazerosize(req);
		getareafromarea(tmpar,0,getarealength(tmpar),req);
	end;

return;
end;

global function string 255 NormalizeStrToJson (string Str)
begin
	integer strln,i;
	string 255 NormStr;
	

	NormStr = "";
	strln = len(Str);
	for (i=0;i<strln;i=i+1)begin
	  if(mid(Str,i,1)==chr(92))then begin
	    NormStr = NormStr & "";
	  end else begin
      if(mid(Str,i,1)==chr(34))then begin
        NormStr = NormStr & chr(92) & mid(Str,i,1);
      end else begin
        NormStr = NormStr & mid(Str,i,1);
      end;
		end;
	end;
	
	
	NormalizeStrToJson = NormStr;
	
	return;
end;


global webpublic procedure WebGetUsersTelegram()
begin
string 50 location;
area resarea;
record UserVc USr,curUSr;
record DepVc Depr;
boolean testf;

  logtext(0,"WebGetUsersTelegram start");
  WebSetContentType("application/json; charset=UTF-8");	

  SetCompany(9,false);
  
  AddTextToArea("[",resarea);
  while(loopmain(USr,1,true))begin
      testf  = true;
      //if(USr.AccessGroup!="SALE" or )then begin testf = false; end;
      if(USr.Closed!=0)then begin testf = false; end;
      if(nonblank(curUSr.SalesGroup) and USr.SalesGroup!=curUSr.SalesGroup)then begin testf = false; end;
      
      if(USr.TelIsBoss==1 and USr.Closed==0)then begin
        testf = true;
      end;
      
      if(testf) then begin
        Depr.Code = USr.Department;
        readfirstmain(Depr,1,true);
        AddTextToArea("{",resarea);
        addParamToJSONArea("code",NormalizeStrToJson(USr.Code),resarea);
        addParamToJSONArea("name",NormalizeStrToJson(USr.Name),resarea);
        addParamToJSONArea("accessgroup",NormalizeStrToJson(USr.AccessGroup),resarea);
        addParamToJSONArea("salesgroup",NormalizeStrToJson(USr.SalesGroup),resarea);
        addParamToJSONArea("locations",NormalizeStrToJson(USr.TelLocation),resarea);
        addParamToJSONArea("phone",NormalizeStrToJson(USr.Phone1),resarea);
        addParamToJSONArea("chatid",NormalizeStrToJson(USr.TelChatId),resarea);
        addParamToJSONArea("accessrules",NormalizeStrToJson(USr.TelIsBoss),resarea);
        delLastComaInArea(resarea);
        AddTextToArea("},",resarea);
      end;
  end;
  delLastComaInArea(resarea);
  AddTextToArea("]",resarea);


  weboutarea2(resarea);
  
  logtext(0,"WebGetUsersTelegram end");
return;
end;


global webpublic updating procedure WebPostUsersTelegram()
begin
string 50 location;
area resarea;
record UserVc USr,curUSr;
record DepVc Depr;
boolean testf;
area postdata;
json jsonobj;

  logtext(0,"WebGetUsersTelegram start");
  WebSetContentType("application/json; charset=UTF-8");	

  SetCompany(9,false);
  
  webgetpostdata(postdata);
  jsonobj = ParseJSONArea(postdata);
  
  USr.Code = JSONGet(jsonobj,"code");
  if(readfirstmain(USr,1,true))then begin
    if(nonblank(JSONGet(jsonobj,"chatid")))then begin
      USr.TelChatId = JSONGet(jsonobj,"chatid");
      testf = true;
    end;
    if(nonblank(JSONGet(jsonobj,"phone")))then begin
      USr.Phone1 = JSONGet(jsonobj,"phone");
      testf = true;
    end;
    if(testf)then begin
      recordstore(USr,true);
      weboutstring("{\"result\":\"ok\"}");
    end;
  end else begin
    weboutstring("{\"result\":\"error\"}");
  end;
  

  
  logtext(0,"WebGetUsersTelegram end");
return;
end;

global webpublic procedure WebGetCLCorspTelegram()
begin
area resarea;
record UserVc User;
record CLCorspVc CLCorspr;

logtext(0,"WebGetCLCorspTelegram start");


	SetCompany(9,false);
	WebSetContentType("application/json; charset=UTF-8");	

  AddTextToArea("[",resarea);
  while(loopmain(CLCorspr,1,true))begin
    if (CLCorspr.Closed==0) then begin
      AddTextToArea("{",resarea);
      addParamToJSONArea("code",NormalizeStrToJson(CLCorspr.Code),resarea);
      addParamToJSONArea("name",NormalizeStrToJson(CLCorspr.Comment),resarea);
      addParamToJSONArea("class",NormalizeStrToJson(CLCorspr.Class),resarea);
      delLastComaInArea(resarea);
      AddTextToArea("},",resarea);
    end;
  end;
  delLastComaInArea(resarea);
  AddTextToArea("]",resarea);


weboutarea2(resarea);

logtext(0,"WebGetCLCorspTelegram end");

return;
end;



global webpublic procedure WebGetLocationsTelegram()
begin
area resarea;
record LocationVc Locr;
string 100 location;
boolean TrHs,TrHs2,testf;
record UserVc USr;
record CLOutVc CLOutr;

logtext(0,"WebGetLocationsTelegram start");

  location = webgetarg("location");
	SetCompany(9,false);
	WebSetContentType("application/json; charset=UTF-8");	

  AddTextToArea("[",resarea);
  Locr.Code = location;
  TrHs = true;
  while(loopmain(Locr,1,TrHs))begin
    if(nonblank(location) and location!=Locr.Code)then begin
      TrHs = false;
    end;
    USr.Code = Locr.Code;
    readfirstmain(USr,1,true);
    if (nonblank(Locr.Group) and TrHs and USr.Closed==0 and nonblank(USr.Code)) then begin
      AddTextToArea("{",resarea);
      addParamToJSONArea("code",NormalizeStrToJson(Locr.Code),resarea);
      addParamToJSONArea("name",NormalizeStrToJson(Locr.Name),resarea);
      addParamToJSONArea("group",NormalizeStrToJson(Locr.Group),resarea);
      addParamToJSONArea("objects",NormalizeStrToJson(Locr.Objects),resarea);
      addParamToJSONArea("balance",NormalizeStrToJson(Locr.BalanceSum),resarea);
      addParamToJSONArea("expenses",NormalizeStrToJson(Locr.RashodSum),resarea);
      addParamToJSONArea("total",NormalizeStrToJson(Locr.BalanceSum - Locr.RashodSum),resarea);
      if(nonblank(location))then begin
        AddTextToArea(" \"daylyExpenses\":[",resarea);
          CLOutr.TransDate = currentdate;
          TrHs = true;
          while(loopkey("TransDate",CLOutr,1,TrHs))begin
            testf = true;
            if(CLOutr.Objects!=USr.PersObjx)then begin testf = false; end;
    
            if(testf)then begin
              AddTextToArea("{",resarea);
              addParamToJSONArea("code",NormalizeStrToJson(CLOutr.SerNr),resarea);
              addParamToJSONArea("type",NormalizeStrToJson(CLOutr.CorspCode),resarea);
              addParamToJSONArea("comment",NormalizeStrToJson(CLOutr.Comment),resarea);
              addParamToJSONArea("sum",NormalizeStrToJson(CLOutr.Total),resarea);
              addParamToJSONArea("date",NormalizeStrToJson(CLOutr.TransDate),resarea);
              delLastComaInArea(resarea);
              AddTextToArea("},",resarea);
            end;
          end;
          delLastComaInArea(resarea);
      
        AddTextToArea("]",resarea);
      end else begin
        delLastComaInArea(resarea);
      end;
      AddTextToArea("},",resarea);
    end;
  end;
  delLastComaInArea(resarea);
  AddTextToArea("]",resarea);


weboutarea2(resarea);

logtext(0,"WebGetLocationsTelegram end");

return;
end;


global webpublic procedure WebGetLastExpTelegram()
begin
area resarea;
record CLOutVc CLOutr;
boolean TrHs,testf;
string 100 object;

logtext(0,"WebGetLastExpTelegram start");
  
  object = webgetarg("object");
  
  
  
	SetCompany(9,false);
	WebSetContentType("application/json; charset=UTF-8");	

  AddTextToArea("[",resarea);
  CLOutr.OKFlag = 0;
  TrHs = true;
  while(loopkey("OKFlag",CLOutr,1,TrHs))begin
    testf = true;
    if(CLOutr.OKFlag!=0)then begin testf = false; TrHs = false; end;
    if(CLOutr.Objects!=object)then begin testf = false; end;
    
    if(testf)then begin
      AddTextToArea("{",resarea);
      addParamToJSONArea("code",NormalizeStrToJson(CLOutr.SerNr),resarea);
      addParamToJSONArea("type",NormalizeStrToJson(CLOutr.CorspCode),resarea);
      addParamToJSONArea("comment",NormalizeStrToJson(CLOutr.Comment),resarea);
      addParamToJSONArea("sum",NormalizeStrToJson(CLOutr.Total),resarea);
      addParamToJSONArea("date",NormalizeStrToJson(CLOutr.TransDate),resarea);
      delLastComaInArea(resarea);
      AddTextToArea("},",resarea);
    end;
  end;
  delLastComaInArea(resarea);
  AddTextToArea("]",resarea);


weboutarea2(resarea);

logtext(0,"WebGetLastExpTelegram end");

return;
end;

global webpublic updating procedure WebPostCreatedCLOutTelegram()
begin
area resarea;
json jsonobj;
record UserVc USr;
record CLInVc CLInr, OldCLInr;
row CLInVc CLInrw;
record CLOutVc CLOutr,OldCLOutr;
row CLOutVc CLOutrw;
area postdata;
boolean TrHs,testf;
val sum;
string 255 user,comment,type,location;
record UserVc US2r;
  
  logtext(0,"WebPostCreatedCLOutTelegram start");
  
  SetCompany(9,false);
    webgetpostdata(postdata);
    jsonobj = ParseJSONArea(postdata);
    
    user = JSONGet(jsonobj,"user");
    location = JSONGet(jsonobj,"location");
    sum = stringtoval(JSONGet(jsonobj,"sum"),m4val);
    comment = JSONGet(jsonobj,"comment");
    type = JSONGet(jsonobj,"type");
    
    if(sum>0)then begin
			recordnew(CLOutr);
			USr.Code = location;
			if(readfirstmain(USr,1,true))then begin
        CLOutr.SerNr = NextSerNr("CLOutVc",CLOutr.TransDate,-1,false,"");
        CLOutr.PayMode = "O";
        CLOutr.ARonTR = 1;
        CLOutr.CurncyCode = "UAH";
        CLOutr.Person = location;
        CLOutr.RefStr = user;
        CLOutVc_PasteCurncyCode(CLOutr);
        CLOutr.Total = sum;
        CLOutr.NetVal = sum;
        CLOutr.TransDate = CurrentDate;
        CLOutr.CorspCode = type;
        CLOutVc_PasteCorspCode(CLOutr,-1);
        CLOutr.Comment = comment;
        CLOutr.Objects = USr.PersObjx;
        recordinsert(CLOutr,true);
        RecordCopy(OldCLOutr,CLOutr);
        CLOutr.OKFlag = 1;
        if (RecordUpdate(OldCLOutr,CLOutr,true)==0) then begin end;
        AddTextToArea("{\"result\":\"ok\"}",resarea);
			end else begin
			  AddTextToArea("{\"result\":\"error\"}",resarea);
			end;
		end else begin
		  AddTextToArea("{\"result\":\"error\"}",resarea);
		end;
		weboutarea2(resarea);
	
	logtext(0,"WebPostCreatedCLOutTelegram end");
	
return;
end;


global webpublic updating procedure WebConfirmLastExpTelegram()
begin
area resarea;
json jsonobj;
record UserVc USr;
record CLInVc CLInr, OldCLInr;
row CLInVc CLInrw;
record CLOutVc CLOutr,OldCLOutr;
row CLOutVc CLOutrw;
area postdata;
boolean TrHs,testf;
val sum;
string 255 user,comment,type,location;
record UserVc US2r;
  
  logtext(0,"WebConfirmLastExpTelegram start");
  
  SetCompany(9,false);
    location = webgetarg("object");
    if(nonblank(location))then begin
      
      TrHs = true;
      CLOutr.OKFlag = 0;
      while(loopkey("OKFlag:" & location,CLOutr,1,TrHs))begin
        if(CLOutr.OKFlag==0) then begin
          logtext(0,"WebConfirmLastExpTelegram " & CLOutr.SerNr);
          recordcopy(OldCLOutr,CLOutr);
          CLOutr.OKFlag = 1;
          if(recordupdate(OldCLOutr,CLOutr,true)==0)then begin
            
          end;
        end else begin
          TrHs = false;
        end;
      end;
      AddTextToArea("{\"result\":\"ok\"}",resarea);
		end else begin
		  AddTextToArea("{\"result\":\"error\"}",resarea);
		end;
		weboutarea2(resarea);
	
	logtext(0,"WebConfirmLastExpTelegram end");
	
return;
end;


//http://65.109.135.110:1080/WebPostCreatedIncasationTelegram.hal


global webpublic updating procedure WebPostCreatedIncasationTelegram()
begin
string 50 location,user;
area resarea;
record CLInVc CLInr, OldCLInr;
row CLInVc CLInrw;
record CLOutVc CLOutr,OldCLOutr;
row CLOutVc CLOutrw;
record IVVc IVr;
boolean TrHs,testf;
val sum;
area postdata;
json jsonobj;
record UserVc USr,US2r;
string 100 saler;
	
	logtext(0,"WebPostCreatedIncasationTelegram start");
	  
	  /*
	  
	  {
    "user":"SA",
    "location":"BROVARY",
    "sum":"1000",
}
	  */
		
		WebSetContentType("application/json; charset=UTF-8");	
		SetCompany(9,false);	

		webgetpostdata(postdata);
		jsonobj = ParseJSONArea(postdata);
		
		user = JSONGet(jsonobj,"user");
		sum = stringtoval(JSONGet(jsonobj,"sum"),m4val);
		location = JSONGet(jsonobj,"location");
				
		if(sum>0 and nonblank(user) and nonblank(location))then begin
			recordnew(CLOutr);
			USr.Code = location;
			readfirstmain(USr,1,true);
			CLOutr.SerNr = NextSerNr("CLOutVc",currentdate,-1,false,"");
			CLOutr.Person = USr.Code;
			CLOutr.RefStr = user;
			CLOutr.CorspCode = "CASH_OUT";
			CLOutr.Comment = "Инкассация " & currentdate & " " & user;
			CLOutr.Total = sum;
			CLOutr.NetVal = sum;
			CLOutr.CurncyCode = "UAH";
			CLOutVc_PasteCurncyCode(CLOutr);
			recordinsert(CLOutr,true);
			RecordCopy(OldCLOutr,CLOutr);
			CLOutr.Objects = USr.PersObjx;
			CLOutr.OKFlag = 1;
			if (RecordUpdate(OldCLOutr,CLOutr,true)==0) then begin end;
			
			recordnew(CLInr);
			USr.Code = user;
			readfirstmain(USr,1,true);
			CLInr.SerNr = NextSerNr("CLInVc",currentdate,-1,false,"");
			CLInr.Person = USr.Code;
			CLInr.RefStr = USr.Code;
			CLInr.CorspCode = "CASH_OUT";
			CLInr.Comment = "Инкассация " & currentdate & " c " & CLOutr.Person;
			CLInr.Total = sum;
			CLInr.CurncyCode = "UAH";
			CLInVc_PasteCurncyCode(CLInr);
			recordinsert(CLInr,true);
			matrowget(CLInr,0,CLInrw);
			RecordCopy(OldCLInr,CLInr);
			CLInr.Objects = USr.PersObjx;
			CLInr.OKFlag = 1;
			if (RecordUpdate(OldCLInr,CLInr,true)==0) then begin end;
			AddTextToArea("{\"result\":\"ok\"}",resarea);
		end else begin
		  AddTextToArea("{\"result\":\"error\"}",resarea);
		end;

		
		weboutarea2(resarea);
	
	
	logtext(0,"WebPostCreatedIncasationTelegram end");
	
return;
end;


/*global webpublic updating procedure WebDeleteLastExpTelegram()
begin
area resarea;
record CLOutVc CLOutr;
boolean TrHs,testf;
string 100 object;

logtext(0,"WebGetLastExpTelegram start");
  
  object = webgetarg("object");
  
  
  
	SetCompany(9,false);
	WebSetContentType("application/json; charset=UTF-8");	

  AddTextToArea("[",resarea);
  CLOutr.OKFlag = 0;
  TrHs = true;
  while(loopkey("OKFlag",CLOutr,1,TrHs))begin
    if(CLOutr.OKFlag==0)then begin 
      recorddelete(CLOutr);
      stepback(CLOutr);
    end else begin
      TrHs = false; 
    end;
    
  end;
  delLastComaInArea(resarea);
  AddTextToArea("]",resarea);



logtext(0,"WebGetLastExpTelegram end");

return;
end;*/

//http://65.109.135.110:1080/WebConfirmLastExpTelegram.hal?object=BOSS

global webpublic updating procedure WebUpdateBalanceForPoints()
begin
  string 50 location,code;
  area resarea;
  record CLInVc CLInr;
  record CLOutVc CLOutr;
  record IVVc IVr;
  boolean TrHs,testf;
  val sum,rashod;
  record Uservc USr;
  vector val vBalance,vRashod;
  record LocationVc Locr;
  
  
  logtext(0,"WebUpdateBalanceForPoints start");
  SetCompany(9,false);
  
  IVr.SerNr = -1;
  while(loopmain(IVr,1,true))begin
    vBalance[IVr.Location] = vBalance[IVr.Location] + IVr.Sum4;
  end;
  
  CLInr.SerNr = -1;
  while(loopmain(CLInr,1,true))begin
    if(CLInr.OKFlag>0) then begin
      vBalance[CLInr.Person] = vBalance[CLInr.Person] + CLInr.Total;
    end;
  end;
  CLOutr.SerNr = -1;
  while(loopmain(CLOutr,1,true))begin
    if(CLOutr.OKFlag>0) then begin
      vBalance[CLOutr.Person] = vBalance[CLOutr.Person] - CLOutr.Total;
    end else begin
      vRashod[CLOutr.Person] = vRashod[CLOutr.Person] + CLOutr.Total;
    end;
  end;
  
  Locr.Code = "";
  while(loopmain(Locr,1,true))begin
    logtext(0,Locr.Code & " vBalance " & vBalance[Locr.Code] & " vRashod " & vRashod[Locr.Code]);
    if(Locr.BalanceSum!=vBalance[Locr.Code] or Locr.RashodSum!=vRashod[Locr.Code])then begin
      weboutstring("+ " & Locr.Code & " ___ " & Locr.BalanceSum & " !=" & vBalance[Locr.Code] & "<BR>");
      Locr.BalanceSum = vBalance[Locr.Code];
      Locr.RashodSum = vRashod[Locr.Code];
      recordstore(Locr,true);
    end;
  end;
  
  logtext(0,"WebUpdateBalanceForPoints end");
return;
end;


global webpublic procedure WebAlert()
begin
  area postdata;
  json jsonobj;
  string 100 alerttime,token;
  
  webgetpostdata(postdata);
  logtext(0,"WebAlert");
  jsonobj = ParseJSONArea(postdata);
    
  alerttime = JSONGet(jsonobj,"time");
  token = JSONGet(jsonobj,"token");
  
  logtext(0,"alerttime " & alerttime);

  if(token=="7964")then begin
    if(alerttime=="hour")then begin
      runprogram("./alertforhr.sh","&");
    end;
    if(alerttime=="day")then begin
      runprogram("./alertforday.sh","&");
    end;
    if(alerttime=="forever")then begin
      runprogram("./alertforever.sh","&");
    end;
  end;
  
return;
end;

