external function Boolean LoggedInTest();


procedure addParamToJSONArea(string param, string value, var area req)
begin

AddTextToArea("\"" & param & "\":" &  "\"" & value & "\",",req);

return;
end;

procedure delLastComaInArea(var area req)
begin
	longint alen;
	string 1 lastsymb;
	area tmpar;
	
	alen = getarealength(req);
	lastsymb = getstringfromarea(req,alen-1,1);
	if(lastsymb==",")then begin
		getareafromarea(req,0,alen-1,tmpar);
		setareazerosize(req);
		getareafromarea(tmpar,0,getarealength(tmpar),req);
	end;

return;
end;

global function string 255 NormalizeStrToJson (string Str)
begin
	integer strln,i;
	string 255 NormStr;
	

	NormStr = "";
	strln = len(Str);
	for (i=0;i<strln;i=i+1)begin
		if(mid(Str,i,1)==chr(34))then begin
			NormStr = NormStr & chr(92) & mid(Str,i,1);
		end else begin
			NormStr = NormStr & mid(Str,i,1);
		end;
	end;
	
	
	NormalizeStrToJson = NormStr;
	
	return;
end;


global webpublic procedure WebIsLogIn()
begin
  Time checkTime;
  array string 5 checkTimeStr;
  string 255 sentMD5,checkMD5;
  integer deltaMin,i,cti;
  boolean isLogedIn;
  
  logtext(0,"WebIsLogIn before " & currentuser);
  
  isLogedIn = WebLoginStatus==3;
  logtext(0,"isLogedIn " & isLogedIn);
  
  if (isLogedIn and nonblank(currentuser)) then begin
  	logtext(0,"WebIsLogIn" & currentuser);
    WebOutString("true");
  end else begin
  	WebOutString("false");
  end;
  
  return;
end;

global webpublic procedure WebGetInventStat()
begin
string 50 location;
record INVc INr;
record ItemStatusVc ISr;
area resarea;

if(WebLoginStatus==3)then begin

	SetCompany(2,false);
	WebSetContentType("application/json; charset=UTF-8");	

	//location = webgetarg("location");
	location = "OSN";
	if(nonblank(location))then begin
	
		AddTextToArea("{",resarea);
		AddTextToArea("\"items\":[",resarea);
		while(loopmain(INr,1,true))begin
			ISr.Code = INr.Code;
			ISr.Location = location;
			if(readfirstmain(ISr,2,true))then begin
				if(ISr.Instock>0)then begin
					AddTextToArea("{",resarea);
					addParamToJSONArea("ArtCode",NormalizeStrToJson(INr.Code),resarea);
					addParamToJSONArea("BarCode",NormalizeStrToJson(INr.BarCode),resarea);
					addParamToJSONArea("AlternativeCode",NormalizeStrToJson(INr.AlternativeCode),resarea);
					addParamToJSONArea("Name",NormalizeStrToJson(INr.Name),resarea);
					addParamToJSONArea("Instock",NormalizeStrToJson(ISr.Instock),resarea);
					addParamToJSONArea("Price",NormalizeStrToJson(INr.UPrice1),resarea);
					delLastComaInArea(resarea);
					AddTextToArea("},",resarea);
				end;
			end;
		end;
		delLastComaInArea(resarea);
		AddTextToArea("]",resarea);
		AddTextToArea("}",resarea);
	
	
	
	end;

end;

weboutarea2(resarea);
return;
end;

global webpublic updating procedure WebSendSale()
begin
	record IVVc IVr;
	row IVVc IVrw;
	integer i,rwcnt;
	area postdata;
	json jsonobj;
	
	if(WebLoginStatus==3)then begin
	
		SetCompany(2,false);
	
		logtext(0,"WebSendSale");
	
		webgetpostdata(postdata);
		writeareatofile(postdata,"WebSendSale.txt",0);
		jsonobj = ParseJSONArea(postdata);
	
		if(nonblank(JSONGet(jsonobj,"SerNr")))then begin
			IVr.SerNr = stringtolongint(JSONGet(jsonobj,"SerNr"));
			if(readfirstmain(IVr,1,true))then begin
			
			end else begin
				recordnew(IVr);
				IVr.SerNr = NextSerNr("IVVc",IVr.TransDate,-1,false,IVr.LangCode);
				IVr.CustCode = "NONAME";
			end;
		
			rwcnt = matrowcnt(IVr);
			if(rwcnt>0)then begin
				for(i=0;i<rwcnt;i=i+1)begin
					matrowdelete(IVr,0);
				end;
			end;
			
			IVr.SalesMan = currentuser;
			
			rwcnt = JSONCountChildren(jsonobj,"items");
			for(i=0;i<rwcnt;i=i+1)begin
				clearrow(IVr,IVrw,kInvoiceRowTypeNormal);
				IVrw.ArtCode = JSONGet(jsonobj,"items/[" & i & "]/ArtCode");
				logtext(0,"IVrw.ArtCode " & IVrw.ArtCode);
				IVrw.Spec = JSONGet(jsonobj,"items/[" & i & "]/Spec");
				IVrw.Quant = evaltoval(JSONGet(jsonobj,"items/[" & i & "]/Quant"));
				IVrw.Price = evaltoval(JSONGet(jsonobj,"items/[" & i & "]/Price"));
				IVrw.Sum = evaltoval(JSONGet(jsonobj,"items/[" & i & "]/Sum"));
				matrowput(IVr,matrowcnt(IVr),IVrw);
			end;
		end;
	
		logtext(0,"IVr.SerNr " & IVr.SerNr);
	
		if(IVr.SerNr>-1)then begin
			recordstore(IVr,true);
		end;
	
		weboutstring("{\"result\":\"ok\",\"SerNr\":\"" & IVr.SerNr & "\"}");
	
	end;
	
 return;
end;

global webpublic procedure WebGetDaySales()
begin
string 50 location;
record INVc INr;
record ItemStatusVc ISr;
area resarea;
record IVVc IVr;
row IVVc IVrw;
boolean TrHs,testf;
integer i,rwcnt;
record UserVc USr;

if(WebLoginStatus==3)then begin

	SetCompany(2,false);
	
	USr.Code = currentuser;
	readfirstmain(USr,1,true);
	location = USr.Location;
	//location = webgetarg("location");
	location = "OSN";
	WebSetContentType("application/json; charset=UTF-8");	

	if(nonblank(location))then begin
		AddTextToArea("{",resarea);
		AddTextToArea("\"invoices\":[",resarea);
		TrHs = true;
		IVr.Location = location;
		while(loopkey("Location",IVr,1,TrHs))begin
			testf = true;
			if(IVr.Location!=location)then begin TrHs = false; testf = false; end;
			if(IVr.SalesMan!=USr.Code)then begin testf = false; end;
			
			logtext(0,IVr.Sernr & "  " & testf);
			
			if(testf)then begin
				AddTextToArea("{",resarea);
					addParamToJSONArea("SerNr",NormalizeStrToJson(IVr.SerNr),resarea);
					addParamToJSONArea("Sum4",NormalizeStrToJson(IVr.Sum4),resarea);
					addParamToJSONArea("OKFlag",NormalizeStrToJson(IVr.OKFlag),resarea);
					addParamToJSONArea("salesman",NormalizeStrToJson(IVr.SalesMan),resarea);
					addParamToJSONArea("TransDate",NormalizeStrToJson(IVr.TransDate),resarea);
					addParamToJSONArea("TransTime",NormalizeStrToJson(IVr.TransTime),resarea);
					addParamToJSONArea("OfficialSerNr",NormalizeStrToJson(IVr.OfficialSerNr),resarea);
					addParamToJSONArea("salesman",NormalizeStrToJson(IVr.SalesMan),resarea);
					AddTextToArea("\"items\":[",resarea);
						rwcnt = matrowcnt(IVr);
						for(i=0;i<rwcnt;i=i+1)begin
							matrowget(IVr,i,IVrw);
							if(IVrw.stp==kInvoiceRowTypeNormal)then begin
								AddTextToArea("{",resarea);
								addParamToJSONArea("ArtCode",NormalizeStrToJson(IVrw.ArtCode),resarea);
								addParamToJSONArea("Spec",NormalizeStrToJson(IVrw.Spec),resarea);
								addParamToJSONArea("Quant",NormalizeStrToJson(IVrw.Quant),resarea);
								addParamToJSONArea("Price",NormalizeStrToJson(IVrw.Price),resarea);
								addParamToJSONArea("vRebate",NormalizeStrToJson(IVrw.vRebate),resarea);
								addParamToJSONArea("Sum",NormalizeStrToJson(IVrw.Sum),resarea);
								delLastComaInArea(resarea);
								AddTextToArea("},",resarea);
							end;
						end;
					delLastComaInArea(resarea);
					AddTextToArea("]",resarea);
				AddTextToArea("},",resarea);
			end;
		end;
	
		delLastComaInArea(resarea);
		AddTextToArea("]",resarea);
		AddTextToArea("}",resarea);
	
	end;

	weboutarea2(resarea);
end;

return;
end;


global webpublic procedure WebGetBarCodes()
begin
string 50 location;
record INVc INr;
record ItemStatusVc ISr;
area resarea;
record BarCodeVc BCr;

	if(WebLoginStatus==3)then begin

		WebSetContentType("application/json; charset=UTF-8");	

		SetCompany(2,false);
	
			AddTextToArea("{",resarea);
			AddTextToArea("\"codes\":[",resarea);
			while(loopmain(BCr,1,true))begin

					AddTextToArea("{",resarea);
					addParamToJSONArea("Barcode",NormalizeStrToJson(BCr.Barcode),resarea);
					addParamToJSONArea("Itemcode",NormalizeStrToJson(BCr.Itemcode),resarea);
					delLastComaInArea(resarea);
					AddTextToArea("},",resarea);
			end;
			delLastComaInArea(resarea);
			AddTextToArea("]",resarea);
			AddTextToArea("}",resarea);
	
	
		weboutarea2(resarea);

	end;

return;
end;


global webpublic procedure WebGetUsers()
begin
string 50 location;
area resarea;
record UserVc USr;
record DepVc Depr;

	if(WebLoginStatus==3)then begin

		WebSetContentType("application/json; charset=UTF-8");	

		SetCompany(2,false);
	
			AddTextToArea("{",resarea);
			AddTextToArea("\"users\":[",resarea);
			while(loopmain(USr,1,true))begin
					Depr.Code = USr.Department;
					readfirstmain(Depr,1,true);
					AddTextToArea("{",resarea);
					addParamToJSONArea("login",NormalizeStrToJson(USr.Code),resarea);
					addParamToJSONArea("name",NormalizeStrToJson(USr.Name),resarea);
					addParamToJSONArea("address",NormalizeStrToJson(Depr.Addr0),resarea);
					addParamToJSONArea("location",NormalizeStrToJson(USr.Location),resarea);
					addParamToJSONArea("phone",NormalizeStrToJson(USr.Phone1),resarea);
					delLastComaInArea(resarea);
					AddTextToArea("},",resarea);
			end;
			delLastComaInArea(resarea);
			AddTextToArea("]",resarea);
			AddTextToArea("}",resarea);
	
	
			weboutarea2(resarea);
	end;

return;
end;


global webpublic procedure WebGetCashDocs()
begin
string 50 location;
area resarea;
record CLInVc CLInr;
record CLOutVc CLOutr;
record IVVc IVr;
boolean TrHs,testf;
val sum;

if(WebLoginStatus==3)then begin

	WebSetContentType("application/json; charset=UTF-8");	

	SetCompany(2,false);
	
		TrHs = true;
		while(loopkey("SerNr:" & "BOSS",IVr,1,TrHs))begin
			testf = true;
			if(testf)then begin 
				sum = sum + IVr.Sum4;
			end;
	
		end;
	
		while(loopkey("SerNr:" & "BOSS",CLInr,1,true))begin
			sum = sum + CLInr.Total;
		end;
		while(loopkey("SerNr:" & "BOSS",CLOutr,1,true))begin
			sum = sum - CLOutr.Total;
		end;
	
		AddTextToArea("{",resarea);
		AddTextToArea("\"balance\":\"",resarea);
		addtexttoarea(sum,resarea);
		AddTextToArea("\"}",resarea);
		weboutarea2(resarea);
	
	
	end;
return;
end;

global webpublic updating procedure WebPutCashDocs()
begin
string 50 location;
area resarea;
record CLInVc CLInr;
record CLOutVc CLOutr;
record IVVc IVr;
boolean TrHs,testf;
val sum;
area postdata;
json jsonobj;
	
	if(WebLoginStatus==3)then begin
		
		WebSetContentType("application/json; charset=UTF-8");	
		SetCompany(2,false);	
		
		
		webgetpostdata(postdata);
		writeareatofile(postdata,"WebSendSale.txt",0);
		jsonobj = ParseJSONArea(postdata);
		
		recordnew(CLOutr);
		CLOutr.SerNr = NextSerNr("CLOutVc",currentdate,-1,false,"");
		recordstore(CLOutr,true);
		
		recordnew(CLInr);
		CLInr.SerNr = NextSerNr("CLInVc",currentdate,-1,false,"");
		recordstore(CLInr,true);
		
		
		AddTextToArea("{\"result\":\"ok\"}",resarea);
		weboutarea2(resarea);
	
	end;
	
return;
end;

