external procedure ExtractObj(string,var Integer,var string);

SetLangMode(LangRussian,"RUS",2); 

global procedure IVToShipRn()
begin
  record IVVc IVr;
  boolean TrHs,testf;
  record CUVc CUr;
  
  startreportnoheaderjob("Отгрузить");
  
    startformat(15);
      outstring(0,0,"Сч/ф",false);
      outstring(60,0,"Дата",false);
      outstring(150,0,"Клиент",false);
      outstring(300,0,"Сумма",false);
      outstring(400,0,"Отгружен",false);
    endformat;
  
    IVr.InvDate = currentdate;
    TrHs = true;
    while(loopbackkey("InvDate",IVr,1,TrHs))begin
      testf = true;
      if(IVr.InvDate<addday(currentdate,-5))then begin TrHs = false; testf = false; end;
      if(IVr.OKFlag==0)then begin testf = false; end;
      CUr.Code = IVr.CustCode;
      readfirstmain(CUr,1,true);
      if(CUr.CustCat!="TELEG")then begin testf = false; end;
      
      if(testf)then begin
        startformat(15);
          outstring(0,"DblIVVc",IVr.SerNr,false);
          outstring(60,0,IVr.InvDate,false);
          outstring(150,0,IVr.Addr0,false);
          outstring(300,0,IVr.Sum4,false);
          if(IVr.ShipedFlag==1)then begin
            outstring(400,0,"X",false);
          end;
          gray_divider(0,1);
          
        endformat;
      end;
    end;
  
  endjob;

return;
end;


function string 60 GetDispGroupType(string DispGroup)
begin
  string 60 res;
  record DIVc DIr;
  DIr.Code = DispGroup;
  if (ReadFirstMain(DIr,1,true)) then begin
    res = DIr.CType;  
  end
  else begin
    res = "";
  end;

  GetDispGroupType = res;
  return 
end;

function string 60 GetDispGroup(string DispGroups, string DispGroupType) 
begin
  string 255 s;
  string 60 grp;
  Integer pos;
  
  s= DispGroups;
  pos = 0;
  ExtractObj(s,pos,grp);
  while (nonblank(grp)) begin
    if (GetDispGroupType(grp)) == DispGroupType then begin // SBU
      GetDispGroup = grp;
      return;
    end;
    ExtractObj(s,pos,grp);
  end; 
  GetDispGroup = "";
  return;
end;

global procedure TelegPriceRn(record RcVc RepSpec)
begin
  record INVc INr;
  record ItemStatusVc ISr;
  record PLVc PLr;
  boolean TrHs,testf;
  string 255 spec,class,classname, group;
  integer price,i,rwcnt;
  record CTypeVc CTyper;
  record ORVc ORr;
  row ORVc ORrw;
  record DIVc DIr;
  vector string 200 vdinames;

  while(loopmain(DIr,1,true))begin
    vdinames[DIr.Code] = DIr.Name;
  end;

  //CTyper.Code = "SIZE";
  //readfirstmain(CTyper,1,true);
  
  startreportnoheaderjob("Телеграм прайс");
    
    PLr.PLCode = "RRP3";
    TrHs = true;
    while(loopmain(PLr,1,TrHs))begin
      testf = true;
      if(PLr.PLCode!="RRP3")then begin TrHs = false; testf = false; end;
      ISr.Code = PLr.ArtCode;
      ISr.Location = "SORTIROVOC";
      readfirstmain(ISr,1,true);

      if(ISr.Instock<=0)then begin testf = false; end;

      if(testf)then begin
        INr.Code = PLr.ArtCode;
        logtext(0,INr.Code);
        readfirstmain(INr,1,true);
        spec = linetextget(INr,0);
        if(blank(spec))then begin
          spec = INr.Name;
        end;
        class = GetDispGroup(INr.DispGroups,"SIZE");
        //startformat(15);
          price = PLr.ExVatPrice;
        //  outstring(0,0,"☁" & spec & " - " & price & " грн",false);
        //endformat;
        clearrow(ORr,ORrw,1);
        ORrw.ArtCode = PLr.ArtCode;
        ORrw.Price = PLr.ExVatPrice;
        ORrw.Spec = spec;
        ORrw.Objects = class;
        matrowput(ORr,matrowcnt(ORr),ORrw);

      end;
    
    end;
    SortRows(ORr,"Objects",true);

    if(matrowcnt(ORr))>0 then begin
      matrowget(ORr,0,ORrw);
      group = ORrw.Objects;
      startformat(15);
        outstring(0,0,"☁" & vdinames[group],false);
      endformat;
    end;
    
    rwcnt = matrowcnt(ORr);
    for (i=0;i<rwcnt;i=i+1) begin
      matrowget(ORr,i,ORrw);

      if(nonblank(ORrw.Objects) and group!=ORrw.Objects)then begin
        group = ORrw.Objects;
        startformat(15);
          outstring(0,0,"⭐" & vdinames[group] & "⭐",false);
        endformat;
      
      end;

      startformat(15);
        outstring(0,0,"☁" & ORrw.Spec & " - " & ORrw.Price & " грн",false);
      endformat;
    end;

  endjob;
  
return;
end;