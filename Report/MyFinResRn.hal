external function roundmode SetRoundModeD(Integer);

SetLangMode(LangRussian,"RUS",0);

global procedure MyFinResRn(record RcVc RepSpec)
begin
  record IVVc IVr;
  row IVVc IVrw;
  integer i,rwcnt;
  boolean TrHs,testf;
  vector val vFinData;
  vector boolean vKeysLoc,vKeysCur;
  val rate;
  record LocationVc Locr;
  Record ObjVc Objr;
  array string 100 akeysLoc,akeysCur;
  record CLOutVc CLOutr;
  
  startreportnoheaderjob("Финансовый отчет");
    
    IVr.InvDate = RepSpec.sStartDate;
    TrHs = true;
    while(LoopKey("InvDate",IVr,1,TrHs))begin
      testf = true;
      if(IVr.InvDate>RepSpec.sEndDate)then begin TrHs = false; testf = false; end;
      if(IVr.OKFlag==0)then begin testf = false; end;
      if(IVr.Invalid!=0)then begin testf = false; end;
      
      if(testf)then begin
        rate = 1;
        if(IVr.ToRateB1!=0)then begin
          rate = IVr.FrRate / IVr.ToRateB1;
        end;
        logtext(0,IVr.SerNr & " IVr.Location " & IVr.Location & " rate " & rate);

        vKeysLoc[IVr.Location] = true;
        vKeysCur[IVr.CurncyCode] = true;
        rwcnt = matrowcnt(IVr);
        for(i=0;i<rwcnt;i=i+1)begin
          matrowget(IVr,i,IVrw);
          if(IVrw.stp==1 and nonblank(IVrw.ArtCode) and IVrw.Quant!=0)then begin
            vFinData[IVr.Location & ":" & IVr.CurncyCode & ":SUM"] = vFinData[IVr.Location & ":" & IVr.CurncyCode & ":SUM"] + IVrw.Sum;
            vFinData[IVr.Location & ":FIFO"] = vFinData[IVr.Location & ":FIFO"] + IVrw.FIFORowVal;
            vFinData[IVr.Location & ":BSUM"] = vFinData[IVr.Location & ":BSUM"] + IVrw.Sum/rate;
            
            if(IVr.PayDeal!="O")then begin
              vFinData["DOLG:" & IVr.Location & ":" & IVr.CurncyCode & ":SUM"] = vFinData["DOLG:" & IVr.Location & ":" & IVr.CurncyCode & ":SUM"] + IVrw.Sum;
              vFinData["DOLG:" & IVr.Location & ":BSUM"] = vFinData["DOLG:" & IVr.Location & ":BSUM"] + IVrw.Sum/rate;
            end;
            
          end;
        end;
      end;
    end;
    
    CLOutr.TransDate = RepSpec.sStartDate;
    TrHs = true;
    while (LoopKey("TransDate",CLOutr,1,TrHs)) begin
    testf = true;
    if (CLOutr.TransDate>RepSpec.sEndDate) then begin TrHs = false;  testf = false; end;
      if(testf)then begin
        rate = 1;
        if(CLOutr.ToRateB1!=0)then begin
          rate = CLOutr.FrRate / CLOutr.ToRateB1;
        end;
        vFinData[CLOutr.Person & ":SPEND:" & CLOutr.CurncyCode] = vFinData[CLOutr.Person & ":SPEND:" & CLOutr.CurncyCode] + CLOutr.Total;
        vFinData[CLOutr.Person & ":SPEND:BSUM"] = vFinData[CLOutr.Person & ":SPEND:BSUM"] + CLOutr.Total/rate;
      end;
    end;
    
    startformat(15);
      outstring(0,0,"Точка",false);
      outstring(120,0,"Выторг (USD)",false);
      outstring(170,0,"(Грн)",false);
      outstring(210,0,"(USD)",false);
      outstring(250,0,"Стоимость (USD)",false);
      outstring(320,0,"Затраты (USD)",false);
      outstring(380,0,"Затраты (ГРН)",false);
      outstring(1,0,"Прибыль",true);
    endformat;
    
    getvectortags(vKeysLoc,akeysLoc);
    for(i=0;i<akeysLoc.length;i=i+1)begin
      Locr.Code = akeysLoc[i];
      if(readfirstmain(Locr,1,true))then begin
        startformat(15);
          outstring(0,0,Locr.Name,false);
          outval(120,0,round(vFinData[Locr.Code & ":BSUM"],DEFAULTCURROUNDOFF),m4val,false);
          outval(170,0,vFinData[Locr.Code & ":UAH:SUM"],m4val,false);
          outval(210,0,vFinData[Locr.Code & ":USD:SUM"],m4val,false);
          outval(250,0,vFinData[Locr.Code & ":FIFO"],m4val,false);
          outval(320,0,round(vFinData[Locr.Code & ":SPEND:BSUM"],DEFAULTCURROUNDOFF),m4val,false);
          outval(380,0,vFinData[Locr.Code & ":SPEND:UAH"],m4val,false);
          outstring(1,0,"Прибыль",true);
        endformat;
      
      end;
    end;
    
    startformat(15);
      outstring(0,0,"Включая продажи в долг",false);
    endformat;
    
    for(i=0;i<akeysLoc.length;i=i+1)begin
      Locr.Code = akeysLoc[i];
      if(readfirstmain(Locr,1,true))then begin
        if(vFinData["DOLG:" & Locr.Code & ":BSUM"]!=0)then begin
          startformat(15);
            outstring(0,0,Locr.Name,false);
            outstring(120,0,"",false);
            outval(170,0,vFinData["DOLG:" & Locr.Code & ":UAH:SUM"],m4val,false);
            outval(210,0,vFinData["DOLG:" & Locr.Code & ":USD:SUM"],m4val,false);
            outstring(250,0,"",false);
            outstring(320,0,"",false);
            outstring(380,0,"",false);
            outstring(1,0,"Прибыль",true);
          endformat;
        end;
      end;
    end;
    
  endjob;
  
return;
end;